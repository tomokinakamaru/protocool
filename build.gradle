plugins {
    id 'java'
    id 'antlr'
    id 'jacoco'
    id 'com.github.ben-manes.versions' version '0.27.0'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id 'com.github.sherter.google-java-format' version '0.8'
}

group 'com.github.tomokinakamaru'

version '0.1.0'

sourceCompatibility = 1.8

targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    antlr 'org.antlr:antlr4:4.7.2'
    implementation 'com.github.tomokinakamaru:fsa:356e36b427'
    implementation 'com.github.tomokinakamaru:antlr4-utilities:687f0ec8ea'
    implementation 'com.github.tomokinakamaru:picocli-utilities:92494503db'
    implementation 'com.google.googlejavaformat:google-java-format:1.7'
    implementation 'com.github.javaparser:javaparser-core:3.15.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.5.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
}

generateGrammarSource {
    def srcPath = source.first().toPath()

    def srcDir = projectDir.toPath().resolve("src").resolve("main").resolve("antlr")

    def pkg = srcDir.relativize(srcPath.parent).toString().replace("/", ".")

    arguments = ['-visitor', '-listener', '-package', "${pkg}".toString()]

    doLast {
        def outDir = outputDirectory.toString()

        def tmpOutDir = outDir + '.tmp'

        copy {
            from outDir
            into tmpOutDir
            filter { line -> line
                    .replaceAll('Class_', 'Class')
                    .replaceAll('Package_', 'Package')
                    .replaceAll('Import_', 'Import') }
        }

        copy {
            from tmpOutDir
            into outDir
        }

        delete tmpOutDir
    }
}

test {
    useJUnitPlatform()
}

jacoco {
    toolVersion = '0.8.4'
}

jacocoTestReport {
    reports {
        xml.enabled = false
        csv.enabled = false
    }
}

check.dependsOn jacocoTestReport

shadowJar {
    manifest.attributes(
            'Main-Class' : "${project.group}.${project.name}.EntryPoint",
            'Version': "${project.version}"
    )
}
